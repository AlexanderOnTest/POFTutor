group 'tech.alexontest'
version '1.0-SNAPSHOT'

apply plugin: 'java'
sourceCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

apply plugin: 'checkstyle'
checkstyle {
    final config_loc = 'config/checkstyle'
    // Adding the line below will cause builds to fail on warning as well as error severity violations
    // maxWarnings = 0
    // Removing the line below will not fail the build on violations, only report results.
    ignoreFailures = false
    configFile = rootProject.file("${config_loc}/checkstyle.xml")
    toolVersion = '8.5'
}

repositories {
    mavenCentral()
}

dependencies {
    def final junitVersion = "5.1.0"
    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    compile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.11.0'
    compile group: 'org.assertj', name: 'assertj-core', version: '3.9.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.7.22'

    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
}

test {
    useJUnitPlatform()
    // use the syntax below to exclude based on filepath
    // exclude(["**/webdriver/*", "**/pagefactory/*"] as List)
    // The syntax below works for Tags - either String names or Tag objects work
    options {
        setExcludeTags(["Framework", "Blocking", "Navigation"] as Set)
    }
    // Finally we have access to parallel running for junit 5 tests!
    maxParallelForks 4
}